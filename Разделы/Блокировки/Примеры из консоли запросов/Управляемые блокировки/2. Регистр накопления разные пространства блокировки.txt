Запрос = Новый Запрос("ВЫБРАТЬ ПЕРВЫЕ 1 Регистратор ИЗ РегистрНакопления.Остаточный");
Выб = Запрос.Выполнить().Выбрать(); Выб.Следующий();
// в одном окне выполнить Транзакцию 1, во втором окне Транзакцию 2
// блокировки не будет, потому что: 
// 	- в первом случае блокируется пространство РегистрНакопления.Остаточный.НаборЗаписей
// 	- во втором случае блокируется пространство РегистрНакопления.Остаточный

// проверить в ТЖ, что пространства разные

// Транзакция 1: блокируем пространство набора записей (RECORDER) по одному из регистраторов
// включить ТЖ с событием TLock, TTimeout для анализа
НачатьТранзакцию();

	Блок = Новый БлокировкаДанных; 
	ЭлемБлокировки = Блок.Добавить("РегистрНакопления.Остаточный.НаборЗаписей"); 
	ЭлемБлокировки.УстановитьЗначение("Регистратор", Выб.Регистратор);
	ЭлемБлокировки.Режим = РежимБлокировкиДанных.Исключительный; 
	
	Блок.Заблокировать();

	Предупреждение("Транзакция 1: 
	|заблокированы записи одного из наборов записей,
	|пространство _AccumRg16.RECORDER (РегистрНакопления.Остаточный.НаборЗаписей)"); 
	
ЗафиксироватьТранзакцию();

// Транзакция 2: блокируем регистр целиком по пространству измерений (DIMS)
НачатьТранзакцию();

	Блок = Новый БлокировкаДанных; 
	ЭлемБлокировки = Блок.Добавить("РегистрНакопления.Остаточный"); 
	ЭлемБлокировки.Режим = РежимБлокировкиДанных.Исключительный; 
	
	Блок.Заблокировать();
	
	Предупреждение("Транзакция 2: 
	|регистр заблокирован ПОЛНОСТЬЮ,  
	|пространство _AccumRg16.DIMS (РегистрНакопления.Остаточный)");

ЗафиксироватьТранзакцию(); 
